<h1>Introduction</h1>

<p>The <code>Bio.SeqLoc</code> modules in <code>seqloc</code> are designed to represent
positions and locations (ranges of positions) on sequences,
particularly nucleotide sequences. My original motivation for writing
these packages was handing the locations of genes in eukaryotic
genomes.</p>

<h1>Strands</h1>

<p>Handling forward and reverse-complement locations and sequences is a
very common task in bioinformatics. The <code>Bio.SeqLoc.Strand</code> package
handles strandedness in <code>seqloc</code>. It consists of a simple <code>Strand</code>
enumerated data type and a <code>Stranded</code> typeclass of any object that has
a strandedness and can therefore be meaningfully reverse complemented.
String-like objects such as <code>String</code> itself and the various
<code>ByteString</code> types instantiate <code>Stranded</code> by reversing the string and
complementing nucleotide characters.</p>

<pre><code>Prelude&gt; :set prompt "ghci&gt; "
ghci&gt; :set -XOverloadedStrings
ghci&gt; import Bio.SeqLoc.Strand
ghci&gt; revCompl "GAttACA"
"TGTaaTC"
ghci&gt; stranded Plus "GATTaca"
"GATTaca"
ghci&gt; stranded Minus "GATTaca"
"tgtAATC"
ghci&gt; stranded (revCompl Plus) "GATTaca"
"tgtAATC"
</code></pre>

<h1>Positions and Offsets</h1>

<pre><code>ghci&gt; import Bio.SeqLoc.Position
</code></pre>

<p>In the <code>seqloc</code> package, an <code>Offset</code> is a 0-based index into a
sequence and a <code>Position</code> is an <code>Offset</code> plus a <code>Strand</code> indicating
the strand on which the position occurs in the sequence.</p>

<h1>Displaying Positions and Locations</h1>

<pre><code>ghci&gt; import Bio.SeqLoc.LocRepr
ghci&gt; :m +Data.ByteString.Char8
</code></pre>

<p>The <code>LocRepr</code> typeclass provides an interface for representing
position and location data types in a format that is easy to read as
well as to parse.  There are two basic functions, <code>repr</code> which
produces a string representation and <code>unrepr</code> which is an extremely
lightweight parser for that string representation from
<code>Data.Attoparsec.Zepto</code>.  There are also helper functions that wrap
the parser and handle errors in different ways.</p>

<pre><code>ghci&gt; repr (Pos 99 Plus)
"99(+)"
ghci&gt; (unreprEither  "99(-)") :: Either String Pos
Right (Pos {offset = Offset {unOffset = 99}, strand = Minus})
ghci&gt; (unreprErr  "99(+)") :: Pos
Pos {offset = Offset {unOffset = 99}, strand = Plus}
</code></pre>

<p>While locations and positions have <code>Show</code> instances, their <code>LocRepr</code>
instances have advantages for human and computer legibility in many
contexts.</p>

<h1>Contiguous Locations</h1>

<pre><code>ghci&gt; import Bio.SeqLoc.Location as Loc
</code></pre>

<p>The <code>ContigLoc</code> type represents a contiguous sequence location, such
as the forward strand from nucleotides 100 to 150, or the reverse
complement strand from nucleotides 1000 to 800. These locations can be
created by specifying their bounds and strand.</p>

<pre><code>ghci&gt; repr $ Loc.fromBoundsStrand 100 150 Plus
"100to150(+)"
ghci&gt; repr $ Loc.fromBoundsStrand 800 1000 Minus
"800to1000(-)"
</code></pre>

<p>They can also be specified with a starting position, which will be the
beginning of the location <em>in its strand</em>, and a length.</p>

<pre><code>ghci&gt; repr $ Loc.fromPosLen (unreprErr "100(+)") 51
"100to150(+)"
ghci&gt; repr $ Loc.fromPosLen (unreprErr "1000(-)") 201
"800to1000(-)"
</code></pre>

<p>Finally, they can be specified from their starting and ending
position, in which case the strand is deduced from the order of the
two positions.</p>

<pre><code>ghci&gt; repr $ Loc.fromStartEnd 100 150
"100to150(+)"
ghci&gt; repr $ Loc.fromStartEnd 1000 800
"800to1000(-)"
</code></pre>

<p>The <code>ContigLoc</code> type is an instance of the <code>Location</code> typeclass, which
provides numerous useful functions.  It's important to remember that
the starting position of a (-) strand location has a higher offset
than the ending position.</p>

<pre><code>ghci&gt; let l = Loc.fromStartEnd 1000 800
ghci&gt; let l' = revCompl l
ghci&gt; Loc.bounds l
(Offset {unOffset = 800},Offset {unOffset = 1000})
ghci&gt; repr $ Loc.startPos l
"1000(-)"
ghci&gt; repr $ Loc.endPos l
"800(-)"
ghci&gt; Loc.bounds l'
(Offset {unOffset = 800},Offset {unOffset = 1000})
ghci&gt; repr $ Loc.startPos l'
"800(+)"
ghci&gt; repr $ Loc.endPos l'
"1000(+)"
</code></pre>

<p>The <code>Location</code> typeclass also allows us to convert between a position
in absolute coordinates and a position relative to a location. The
<code>posInto</code> function takes an absolute position <em>into</em> a
location-relative position. It may fail, with <code>Nothing</code>, if the
position is outside the location.</p>

<pre><code>ghci&gt; let p = Pos 850 Plus
ghci&gt; maybe "n/a" repr $ Loc.posInto p l
"150(-)"
ghci&gt; maybe "n/a" repr $ Loc.posInto p l'
"50(+)"
ghci&gt; let p2 = Pos 750 Plus
ghci&gt; maybe "n/a" repr $ Loc.posInto p2 l
"n/a"
</code></pre>

<p>The <code>posOutof</code> function pulls a location-relative position back out of
the location.</p>

<pre><code>ghci&gt; let q = Pos 120 Plus
ghci&gt; maybe "n/a" repr $ Loc.posOutof q l
"880(-)"
ghci&gt; maybe "n/a" repr $ Loc.posOutof q l'
"920(+)"
ghci&gt; let q2 = Pos 220 Plus
ghci&gt; maybe "n/a" repr $ Loc.posOutof q2 l'
"n/a"
</code></pre>

<p>Entire locations can be mapped back in a similar way. Here we find a
sub-location from nucleotides 100 through 150 within the enclosing
location of 1000 to 800, map the sub-location back to its absolute
coordinates, and then find its relative coordinates within the
complementary location.</p>

<pre><code>ghci&gt; let k = Loc.fromStartEnd 100 150
ghci&gt; maybe "n/a" repr $ Loc.clocOutof k l
"850to900(-)"
ghci&gt; maybe "n/a" repr $ Loc.clocInto (unreprErr "850to900(-)") l'
"50to100(-)"
</code></pre>

<h1>Sequence Data</h1>

<pre><code>ghci&gt; import Bio.SeqLoc.SeqLike as SeqLike
</code></pre>

<p>The <code>SeqLike</code> typeclass in the <code>Bio.SeqLoc.SeqLike</code> module has a
simple interface to allow the extraction of subsequences based on
locations. There are instances for <code>String</code> and for lazy and strict
<code>ByteString</code> types. Recall that offsets are all 0-based indices and
that <code>fromStartEnd</code> creates a location that includes both endpoints.</p>

<pre><code>ghci&gt; Loc.seqData "GATTACA" (Loc.fromStartEnd 2 4)
Just "TTA"
ghci&gt; Loc.seqData "GATTACA" (Loc.fromStartEnd 2 8)
Nothing
ghci&gt; Loc.seqDataPad "GATTACA" (Loc.fromStartEnd 2 8)
"TTACANN"
ghci&gt; Loc.seqDataPad "GATTACA" (Loc.fromStartEnd (-2) 4)
"NNGATTA"
ghci&gt; Loc.seqDataPad "GATTACA" (Loc.fromStartEnd 6 0)
"TGTAATC"
</code></pre>

<p>The instances for <code>String</code> and lazy <code>ByteString</code> avoid evaluating the
full sequence whenever possible, but the use of functions such as
<code>length</code> will force its evaluation.</p>

<h1>Spliced Locations</h1>

<pre><code>ghci&gt; import Bio.SeqLoc.SpliceLocation as SpLoc
</code></pre>

<p>The <code>SpliceLoc</code> type in the <code>Bio.SeqLoc.SpliceLocation</code> package
provides spliced locations, designed to model the structure of
eukaryotic genes as a series of individual <code>ContigLoc</code> locations lying
in order on the same strand.</p>

<pre><code>ghci&gt; maybe "n/a" repr $ SpLoc.fromContigs [ Loc.fromStartEnd 100 150, Loc.fromStartEnd 200 250 ]
"100to150(+);200to250(+)"
ghci&gt; maybe "n/a" repr $ SpLoc.fromContigs [ Loc.fromStartEnd 100 150, Loc.fromStartEnd 250 200 ]
"n/a"
ghci&gt; maybe "n/a" repr $ SpLoc.fromContigs [ Loc.fromStartEnd 100 150, Loc.fromStartEnd 50 80 ]
"n/a"
</code></pre>

<p><code>SpliceLoc</code> implements the <code>Location</code> interface as well. When pulling
a contiguous location out of a spliced location, the result may also
be spliced. When pushing a contiguous location into a spliced
location, it must fit entirely within a single segment of the spliced
location.</p>

<pre><code>ghci&gt; let (Just s) = SpLoc.fromContigs [ Loc.fromStartEnd 100 150, Loc.fromStartEnd 200 250 ]
ghci&gt; maybe "n/a" repr $ Loc.clocOutof (Loc.fromStartEnd 25 75) s
"125to150(+);200to224(+)"
ghci&gt; maybe "n/a" repr $ Loc.clocInto (Loc.fromStartEnd 210 240) s
"61to91(+)"
ghci&gt; maybe "n/a" repr $ Loc.clocInto (Loc.fromStartEnd 190 240) s
"n/a"
</code></pre>

<p>The module also provides specialized functions for finding the
coordinates of spliced locations relative to an enclosing spliced
location. These will properly merge locations whose relative
coordinates are adjacent</p>

<pre><code>ghci&gt; let (Just u) = Loc.clocOutof (Loc.fromStartEnd 20 81) s
ghci&gt; repr u
"120to150(+);200to230(+)"
ghci&gt; let (Just t) = SpLoc.fromContigs [ Loc.fromStartEnd 120 150, Loc.fromStartEnd 200 230 ]
ghci&gt; maybe "n/a" repr $ SpLoc.locInto t s
"20to81(+)"
ghci&gt; let (Just t') = SpLoc.fromContigs [ Loc.fromStartEnd 120 150, Loc.fromStartEnd 210 230 ]
ghci&gt; maybe "n/a" repr $ SpLoc.locInto t' s
"20to50(+);61to81(+)"
</code></pre>

<h1>Named Sequences</h1>

<pre><code>ghci&gt; import Bio.SeqLoc.OnSeq
ghci&gt; import qualified Data.ByteString.Char8 as BS
</code></pre>

<p>Genome annotation data files typically express the location of a gene
as a [spliced] location on one of several chromosomes. The <code>OnSeq</code>
type in the <code>Bio.SeqLoc.OnSeq</code> allows position and location types to
be tagged with names. The module provides useful type synonyms for the
named location data types.</p>

<pre><code>ghci&gt; let z = OnSeq (toSeqLabel "chr1") (Loc.fromStartEnd 10000 20000)
ghci&gt; repr z
"chr1@10000to20000(+)"
ghci&gt; repr $ revCompl z
"chr1@10000to20000(-)"
</code></pre>
